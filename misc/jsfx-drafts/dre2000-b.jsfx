slider1:decay_p=0.8<0,1,0.01> Decay

slider3:mod=0.5<0,1,0.01> Mod
slider4:character=0.5<0,1,0.01> Character

slider6:hi=0.5<0,1,0.01> highs
slider7:lo=0.5<0,1,0.01> lows

slider9:stereo=1<-1,1, 0.01> Stereo
slider10:predelay=1<1, 2400, 1> Predelay

slider21:db_wet=-12<-40, 12,0.1> Wet dB
slider20:db_dry=0<-60, 0,0.1> Dry dB

slider23:offset=0<0, 30, 1> Offset

import _core.jsfx-inc
import _easy_allpass_reverb.jsfx-inc
import _easy_fdn.jsfx-inc
import _reverb.jsfx-inc
import _lfo.jsfx-inc
import _fir.jsfx-inc

@init

// SRC stuff -------------------------------------------------------------------
BYPASS_SRC = 0;

k.src.kaiser_att_db = 210;
k.src.taps_branch = 32;
k.src.taps_branch_frac = 16;
k.src.cutoff = 15000;
k.src.srate = BYPASS_SRC ? srate : 32400;

src.down.resampler_2c_init(
  k.src.srate,
  srate,
  k.src.taps_branch,
  k.src.taps_branch_frac,
  k.src.cutoff,
  k.src.kaiser_att_db,
  1
  );

src.up.resampler_2c_init(
  srate,
  k.src.srate,
  k.src.taps_branch,
  k.src.taps_branch_frac,
  k.src.cutoff,
  k.src.kaiser_att_db,
  1
  );

src.out_q.deque_init(
  src.up.resampler_2c_max_n_out_samples() * 2
  );

src.ins.array_init (src.down.resampler_2c_max_n_out_samples() * 2);
src.outs.array_init (src.up.resampler_2c_max_n_out_samples() * 2);

src.ins.array_clear();
src.outs.array_clear();

mtx.array_init (4);
mtxcp.array_init (4);

// TODO how many?
loop(floor (srate / 44100) * 2,
  src.out_q.deque_push (0);
  src.out_q.deque_push (0);
);

// Actual reverb stuff ---------------------------------------------------------
n_lfos = 4;
lfo.lfo_bank_init (n_lfos, 1);
lfo.ret.array_init (n_lfos);
lfo.lfo_bank_reset_phase (0);

@slider

lfo.lfo_bank_reset_freq (0, 0.25 + mod * .15, k.src.srate);

dry_gain  = (dry_gain == -60) ? 0. : db_to_lin (db_dry);
wet_gain = db_to_lin (db_wet);

decayfb = 0.2 + decay_p * 0.7;

sumgain =  0.125;

flo = 0.90 + lo * lo * 0.05;
glo = 0.7 + lo * 0.3;
fhi = 0.82 - hi * hi * 0.4;
ghi = 0.5 + hi * 0.25;

@sample

// Downsampling
n_spls_down = src.down.resampler_2c_tick (src.ins.ptr, spl0, spl1);

(n_spls_down) ? (
  m_lfo.lfo_bank_tick_tri (m_lfo.ret.ptr, 0);
  lfo.lfo_bank_tick_tri (lfo.ret.ptr, 0);

  // https://gearspace.com/board/attachments/geekzone/1002937d1644118238-reverb-subculture-v2_reverbb.jpg


  // halving an additional time to leave headroom
  m = (src.ins.ptr[0] + src.ins.ptr[1]) * 0.5 * 0.5;
  m = fi1.lp (m, 0.01);
  m = fi2.hp (m, 0.99);
  ml = m;


// block1
// [[283, 293], [163, 167], [113, 127], [83, 89], [71, 73], 377]
// [288.4793102880037, 163.4261175325046, 114.00567868751925, 87.53491605246329, 71.040221545051, 377]


  ci = ml + fb1;
  fb1 = fb1.crossv2 (ci, flo, fhi, glo, ghi);
  fb1 = delay (fb1, 3821 - 32, lfo.ret.ptr[0] * mod, 54) * -decayfb;
//  fb1 = delay (fb1, 3789) * -decayfb;
  fb1 = delay (fb1, 32);
  t = delay (ci, 429) * -sumgain; // notice assign not sum
  t += delay (ci, 1000) * sumgain;
  t += delay (ci, 1472) * -sumgain;
  t += delay (ci, 2088) * sumgain;
  t += delay (ci, 2765) * -sumgain;
  t += delay (ci, 3311) * sumgain;

  ci = ml + fb2;
  fb2 = fb2.crossv2 (ci, flo, fhi, glo, ghi);
  fb2 = delay (fb2, 4036 -32, lfo.ret.ptr[1] * mod, 53) * decayfb;
//  fb2 = delay (fb2, 4004) * decayfb;
  fb2 = delay (fb2, 32);
  t += delay (ci, 616) * -sumgain;
  t += delay (ci, 1225) * sumgain;
  t += delay (ci, 1691) * -sumgain;
  t += delay (ci, 2434) * sumgain;
  t += delay (ci, 3122) * -sumgain;
  t += delay (ci, 3631) * sumgain;

  ci = ml + fb3;
  fb3 = fb3.crossv2 (ci, flo, fhi, glo, ghi);
  fb3 = delay (fb3, 4059 -32, lfo.ret.ptr[2] * mod, 52) * -decayfb;
  fb3 = delay (fb3, 32);
  t += delay (ci, 657) * -sumgain;
  t += delay (ci, 1359) * sumgain; // notice assign not sum
  t += delay (ci, 2184) * -sumgain;
  t += delay (ci, 2744) * sumgain;
  t += delay (ci, 3411) * -sumgain;
  t += delay (ci, 3934) * sumgain;

  t = ap (t, 282, -0.7);
  t = ap (t, 343, -0.7);

  eramt = abs ((character - 0.5)) * 2;
  eramt = 1 - eramt * eramt;
  eramt = 0.4 * eramt;
  mainamt = 1 - eramt;

  l = t;
  l = delay (ml, 311, character, 311) * eramt + l * mainamt;
//  l = delay (ml, 577, character, 311) * 0.125 + l * 0.875;
  l = ap (l, 233, -0.7);
  l = ap (l, 273, -0.7);
//  l = ap (l, 534, -0.7, lfo.ret.ptr[2] * mod, 22);
  l = ap (l, 534, -0.7);

  r = t;
  r = delay (ml, 277, character, 400) * eramt + r * mainamt;
//  r = delay (ml, 311, character, 400) * 0.125 + r * 0.875;
  r = ap (r, 194, -0.7);
  r = ap (r, 426, -0.7);
//  r = ap (r, 566, -0.7, lfo.ret.ptr[3] * mod, 17);
  r = ap (r, 566, -0.7);

  // more boilerplate (summing + downsample)
  easy_ap_next_sample();
  easy_fdn_next_sample();

  l = r * (1 - abs (stereo)) + l * abs (stereo);
  stereo < 0 ? (
    tmp = r;
    r = l;
    l = tmp;
  );

  // Upsampling
  n_spls_up = src.up.resampler_2c_tick (src.outs.ptr, l, r);
  i = 0;
  loop (n_spls_up * 2, // *2 : two channels
    src.out_q.deque_push (src.outs.ptr[i]);
    i += 1;
  );
);

spl0 *= dry_gain;
spl1 *= dry_gain;
spl0 += src.out_q.deque_pop() * wet_gain;
spl1 += src.out_q.deque_pop() * wet_gain;
