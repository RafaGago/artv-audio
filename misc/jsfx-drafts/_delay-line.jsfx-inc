import _core.jsfx-inc

@init

// this might belong somewhere else
function interp_catmull_rom (y0, y1, y2, y3, x)
  local (r, x2, x3) global()(
  x2 = x * x;
  x3 = x2 * x;

  r = x3 * (-y0 + 3 * y1 - 3 * y2 + y3) * 0.5;
  r += x2 * (2 * y0 - 5 * y1 + 4 * y2 - y3) * 0.5;
  r += x * (-y0 + y2) * 0.5;
  r += y1;
);

function delay_line_init (size, n_channels) global() (
    size |= 0;
    this.arr.array_init (size * n_channels);
    this.n_channels = n_channels;
    this.mask = size - 1;
    this.pos = this.mask;
);

function _delay_line_get_pos (pos, channel)
    local (offset) global()(
    offset = channel * (this.mask + 1);
    offset + ((this.pos + pos) & this.mask);
);

function delay_line_set (value, pos, channel) (
    this.arr.ptr[this._delay_line_get_pos (pos, channel)] = value;
);

function delay_line_set_all (ptr, pos) local (i) global()(
    i = 0;
    loop (this.n_channels,
        this.delay_line_set (ptr[i], pos, i);
        i += 1;
    );
);

function delay_line_get (pos, channel) global()(
    this.arr.ptr[this._delay_line_get_pos (pos, channel)];
);

function delay_line_get_all (dst_ptr, times_ptr)
    local (i) global()(
    i = 0;
    loop (this.n_channels,
        dst_ptr[i] = this.delay_line_get (times_ptr[i], i);
        i += 1;
    );
);

function delay_line_get_linterp (pos, channel)
    local (a, b, pos_int, pos_frac) global()(
    pos = clamp (pos, 0, this.mask - 1);
    pos_int = floor (pos);
    pos_frac = pos - pos_int;
    a = this.delay_line_get (pos_int, channel);
    b = this.delay_line_get (pos_int + 1, channel);
    a + pos_frac * (b - a);
);

function delay_line_get_linterp_all (dst_ptr, times_ptr)
    local (i) global()(
    i = 0;
    loop(this.n_channels,
        dst_ptr[i] = this.delay_line_get_linterp (times_ptr[i], i);
        i += 1;
    );
);

function delay_line_get_catmull_rom_interp (pos, channel)
    local (a, b, c, d, pos_int, pos_frac) global()(

    pos = clamp (pos, 0, this.mask - 3);
    pos_int = floor (pos);
    pos_frac = pos - pos_int;
    a = this.delay_line_get (pos_int, channel);
    b = this.delay_line_get (pos_int + 1, channel);
    c = this.delay_line_get (pos_int + 2, channel);
    d = this.delay_line_get (pos_int + 3, channel);
    interp_catmull_rom (a, b, c, d, pos_frac);
);

function delay_line_get_catmull_rom_interp_all (dst_ptr, times_ptr)
  local (i) global()(
    i = 0;
    loop(this.n_channels,
        dst_ptr[i] = this.delay_line_get_catmull_rom_interp (times_ptr[i], i);
        i += 1;
    );
);

function delay_line_get_allpass (in, chnl, g_back, g_fwd, delay_spls)
  global() local (y, yn, out) (

  yn = this.delay_line_get (delay_spls, chnl);
  y  = in + yn * g_back; // feedback
  this.delay_line_set (y, 0, chnl);
  yn - y * g_fwd; // feedforward
);

function delay_line_get_allpass_lerp (in, chnl, g_back, g_fwd, delay_spls)
  global() local (y, yn, out) (

  yn = this.delay_line_get_linterp (delay_spls, chnl);
  y  = in + yn * g_back; // feedback
  this.delay_line_set (y, 0, chnl);
  yn - y * g_fwd; // feedforward
);

function delay_line_advance() global()(
    this.pos -= 1;
    this.pos < 0 ? (this.pos = this.mask);
);
//------------------------------------------------------------------------------
// thiran order 2 interpolation addon/inheritance/monkey patching to a
//  "delay_line". Usage:
//
// del.delay_line_init (size, samples);
// del.delay_line_thiran2_add (100000);

// "delta_sync" number of samples distane between consecutive calls that will
// make the previous states of the thiran biquad to be recalculated. In practice
// as the Z states are always correct the filter doesn't really blow up.
function delay_line_thiran2_add (delta_sync) global()(
    // each dynamic thiran array uses these indexes.
    // 0 = a1,
    // 1 = a2,
    // 2 = y1,
    // 3 = y2,
    // 4 = time_spls
    // 5 = n_coeffs/size
    this.thiran.array_init (this.n_channels * 5);
    this.thiran.delta_sync = delta_sync;
);

function delay_line_thiran2_get (pos, channel)
    local (z0, z1, z2, pos_int, pos_frac, diff, thy_ptr, dlt, y, d)
    global()(
    // each dynamic thiran array uses these indexes.
    // 0 = a1,
    // 1 = a2,
    // 2 = y1,
    // 3 = y2,
    // 4 = time_spls
    // 5 = n_coeffs/size
    thy_ptr = this.thiran.ptr + channel * 5;

    pos = clamp (pos, 0, this.mask - 2);
    pos_int = floor (pos);
    pos_frac = pos - pos_int;

    z0 = this.delay_line_get (pos_int, channel);
    z1 = this.delay_line_get (pos_int + 1, channel);
    z2 = this.delay_line_get (pos_int + 2, channel);

    // delay
    diff = abs (pos - thy_ptr[4]); // time_spls
    (diff != 0) ? (
        // biquad recalc
        // according to this. Table 3.2:
        // http://users.spa.aalto.fi/vpv/publications/vesan_vaitos/ch3_pt3_allpass.pdf
        // The point minimizing the avg error for D0 (D0 < D < D0 +1) is 1.403

        d = pos_frac + 2;
        d = max (pos_frac, 2.0001); // d = delta
        thy_ptr[0] = -2 * (d - 2) / (d + 1); // a1
        thy_ptr[1] = ((d - 1) * (d - 2)) / ((d + 1)*(d + 2)); // a2
        // b0 = a2
        // b1 = a1
        // b2 = 1
        thy_ptr[4] = pos; // time_spls

        (diff > this.delta_sync) ? (
            // resync. Try to approximate to the current state by lerp, it could
            // also be done by running some stages on the delay line or by
            // running two iterations from zero.
            //
            // It seems that in a delay line context as z1 and z2 are always
            // correct the filter doesn't explode.
            //
            // This could be improved to e.g. catmull rom or some iterations
            // with previous samples.
            thy_ptr[2] = z0 + pos_frac * (z1 - z0); // y1
            thy_ptr[3] = z1 + pos_frac * (z2 - z1); // y2
        );
    );

    // Direct form1, otherwise it wouldn't take the past states (z's), which
    // might be more stable for delay line modulation.
    y = z0 * thy_ptr[1]; // a2(==b0) * z0
    y += z1 * thy_ptr[0]; // a1(==b1) * z0
    y += z2; // b2 == 1
    y -= thy_ptr[2] * thy_ptr[0]; // y1 * a1
    y -= thy_ptr[3] * thy_ptr[1]; // y2 * a2
    thy_ptr[3] = thy_ptr[2]; // y2 = y1
    thy_ptr[2] = y; // y1 = y
    clamp (y, -5, 5);
);

function delay_line_thiran2_get_all (dst_ptr, times_ptr)
    global() local (i)(
    i = 0;
    loop(this.n_channels,
        dst_ptr[i] = this.delay_line_thiran2_get (times_ptr[i], i);
        i += 1;
    );
);
//------------------------------------------------------------------------------
// schroeder allpass
//
// "delta_sync" number of samples distane between consecutive calls that will
// make the previous states of the thiran biquad to be recalculated. In practice
// as the Z states are always correct the filter doesn't really blow up.
//
function delay_line_allpass_add ()
    global()(
    this.g.array_init (this.n_channels);
);

function delay_line_allpass_set_g (coeff, channel)
   global()(
   this.g.ptr[channel] = coeff;
);

function delay_line_allpass_set_g_all (src_ptr) local (i)(
    i = 0;
    loop (this.n_channels,
        this.delay_line_allpass_set_g (src_ptr[i], i);
        i += 1;
    );
);

function delay_line_allpass_set_g_k_all (v) local (i)(
    i = 0;
    loop (this.n_channels,
        this.delay_line_allpass_set_g (v, i);
        i += 1;
    );
);

function freq_to_allpass_g (freq, samplerate)
    local (coeff) global()(
    coeff = tan ($pi * freq / samplerate);
    coeff = (1 - coeff) / (1 + coeff);
    coeff = clamp (coeff, 0, 1);
    coeff;
);

function delay_line_allpass_get (in, chnl, delay_spls)
  global() (
    this.delay_line_get_allpass(
        in, chnl, this.g.ptr[chnl], this.g.ptr[chnl], delay_spls
        );
);

function delay_line_allpass_get_lerp (in, chnl, delay_spls)
  global() (
    this.delay_line_get_allpass_lerp(
        in, chnl, this.g.ptr[chnl], this.g.ptr[chnl], delay_spls
        );
);

// no interpolation
// dst_ptr = outs, in_ptr = ins, delay_spls_ptr = delays.
function delay_line_allpass_tick_cascade_all (in, active_count, delay_spls_ptr)
    local (i, out) global() (

    this.delay_line_advance();
    i = 0;
    out = in;
    loop (active_count,
      out = this.delay_line_allpass_get (out, i, delay_spls_ptr[i]);
      i += 1;
    );
    loop (this.n_channels - active_count,
      this.delay_line_allpass_get (0, i, delay_spls_ptr[i]);
      i += 1;
    );
    out;
);

// linear interpolation
// dst_ptr = outs, in_ptr = ins, delay_spls_ptr = delays.
function delay_line_allpass_tick_cascade_all_lerp(
    in, active_count, delay_spls_ptr
    ) local (i, out) global() (

    this.delay_line_advance();
    i = 0;
    out = in;
    loop (active_count,
      out = this.delay_line_allpass_get_lerp (out, i, delay_spls_ptr[i]);
      i += 1;
    );
    loop (this.n_channels - active_count,
      this.delay_line_allpass_get_lerp (0, i, delay_spls_ptr[i]);
      i += 1;
    );
    out;
);

// no interpolation
// dst_ptr = outs, in_ptr = ins, delay_spls_ptr = delays.
function delay_line_allpass_tick_parallel_all (out_ptr, in_ptr, delay_spls_ptr)
    local (i) global() (

    this.delay_line_advance();
    i = 0;
    loop (this.n_channels,
      out_ptr[i] =
        this.delay_line_allpass_get (in_ptr[i], i, delay_spls_ptr[i]);
      i += 1;
    );
);

// no interpolation
// dst_ptr = outs, in_ptr = ins, delay_spls_ptr = delays.
function delay_line_allpass_tick_tapped_all (in, active_count, delay_spls_ptr)
    local (i, next, out) global() (

    this.delay_line_advance();
    i = 0;
    next = in;
    out = 0;
    loop (active_count,
      next = this.delay_line_allpass_get (next, i, delay_spls_ptr[i]);
      out += next;
      i += 1;
    );
    loop (this.n_channels - active_count,
      this.delay_line_allpass_get (0, i, delay_spls_ptr[i]);
      i += 1;
    );
    out / active_count;
);

function delay_line_allpass_tick_tapped_cascade_all(
    in, count, count_tapped, delay_spls_ptr
    )
    local (i, next, out) global() (

   this.delay_line_advance();
    i = 0;
    next = in;
    out = in;
    loop (count_tapped,
      next = this.delay_line_allpass_get (next, i, delay_spls_ptr[i]);
      out += next;
      i += 1;
    );
    loop (count,
      out = this.delay_line_allpass_get (out, i, delay_spls_ptr[i]);
      i += 1;
    );
    loop (this.n_channels - (count_tapped + count),
      this.delay_line_allpass_get (0, i, delay_spls_ptr[i]);
      i += 1;
    );
    out / (count_tapped + 1);
);
//------------------------------------------------------------------------------
// object version of the Schroeder above
function schroeder_allpass (max_delay_spls)(
    this.delay_line_init (round_next_pow2 (max_delay_spls), 1);
    this.delay_line_allpass_add();
);

function schroeder_allpass_set_g (coeff)(
    this.delay_line_allpass_set_g (coeff, 0);
);

function schroeder_allpass_tick (in, delay_spls) global (_libmem*) (
    _libmem.ptr[0] = delay_spls;
    _libmem.ptr[1] = 0;
    this.delay_line_allpass_tick_cascade_all (in, 1, _libmem.ptr);
);
//------------------------------------------------------------------------------
// Adds a nested allpass delay line.
//
// Depth is the number of serial allpasses inside the nesting. The regular
// nested allpass would be of depth = 1.
//
// The number of effective channels of the delay line gets divided by "1 +
// depth". When passing the number of delays in samples, the order is
// external delay + N nestings.
//
// E.g, this creates 1 channel of a nested allpass of 1 + 3 elements.
//
// nest_ap.delay_line_init (1024, 4);
// nest_ap.delay_line_nested_allpass_add (3);
//
// To set "g" use the non-nested allpass functions.

function delay_line_nested_allpass_add (depth)
    global() (
    this.delay_line_allpass_add();
    this.n_nested = depth;
    this.n_channels_nested = this.n_channels / (depth + 1);
);

function delay_line_nested_allpass_tick (in, chnl, delay_spls_ptr)
  global() local (ch_idx, g, y, yn, i) (

  ch_idx = chnl * (this.n_nested + 1);

  g  = this.g.ptr[ch_idx];
  yn = this.delay_line_get (delay_spls_ptr[0], ch_idx);

  i = 1;
  loop (this.n_nested,
    yn = this.delay_line_allpass_get (yn, ch_idx + i, delay_spls_ptr[i]);
    i += 1;
  );

  y  = in + yn * g; // feedback
  this.delay_line_set (y, 0, ch_idx);
  yn - y * g; // feedforward
);

// no interpolation
// dst_ptr = outs, in_ptr = ins, delay_spls_ptr = delays.
function delay_line_nested_allpass_tick_cascade_all(
    in, active_count, delay_spls_ptr
    )
    local (i, offs, out) global() (

    this.delay_line_advance();
    i = 0;
    out = in;
    offs = 0;
    loop (active_count,
      out = this.delay_line_allpass_get (out, i, delay_spls_ptr + offs);
      offs += this.n_channels_nested;
      i += 1;
    );
    loop (this.n_channels_nested - active_count,
      this.delay_line_allpass_get (0, i, delay_spls_ptr + offs);
      offs += this.n_channels_nested;
      i += 1;
    );
    out;
);

function delay_line_nested_allpass_tick_parallel_all(
    out_ptr, in_ptr, delay_spls_ptr
    )
    local (i, offs) global() (

    this.delay_line_advance();
    i = 0;
    offs = 0;
    loop (this.n_channels_nested,
      out_ptr[i] = this.delay_line_nested_allpass_tick(
          in_ptr[i], i, delay_spls_ptr + offs
          );
      offs += this.n_channels_nested;
      i += 1;
    );
);
//------------------------------------------------------------------------------
// LP comb as in the freeverb comb
function delay_line_lp_comb_add()
    global()(
    this.g.array_init (this.n_channels);
    this.damp.array_init (this.n_channels);
    this.filt_state.array_init (this.n_channels);
);

function delay_line_lp_comb_set_g (g, channel)
   global()(
   this.g.ptr[channel] = g;
);

function delay_line_lp_comb_set_g_all (src_ptr) local (i)(
    i = 0;
    loop (this.n_channels,
        this.delay_line_lp_comb_set_g (src_ptr[i], i);
        i += 1;
    );
);

function delay_line_lp_comb_set_damp (damp, channel)
   global()(
   this.damp.ptr[channel] = damp;
);

function delay_line_lp_comb_set_damp_all (src_ptr) local (i)(
    i = 0;
    loop (this.n_channels,
        this.delay_line_lp_comb_set_damp (src_ptr[i], i);
        i += 1;
    );
);

// no interpolation
// dst_ptr = outs, in_ptr = ins, delay_spls_ptr = delays.
function delay_line_lp_comb_tick (dst_ptr, in_ptr, delay_spls_ptr)
    local (g, damp, y, lp, i) global()(

    this.delay_line_advance();
    i = 0;
    loop (this.n_channels,
        g    = this.g.ptr[i];
        damp = this.damp.ptr[i];
        lp   = this.filt_state[i];

        y  = this.delay_line_get (delay_spls_ptr[i], i);
        lp = y * (1 - damp) + lp * damp;
        this.delay_line_set (in_ptr[i] + lp * g, 0, i);

        this.filt_state[i] = lp;
        dst_ptr[i] = y;
        i += 1;
    );
);

function delay_line_lp_comb_tick (in, chnl, delay_spls)
  global() local (g, damp, y, lp) (

  g    = this.g.ptr[chnl];
  damp = this.damp.ptr[chnl];
  lp   = this.filt_state[chnl];

  y  = this.delay_line_get (delay_spls, chnl);
  lp = y * (1 - damp) + lp * damp;
  this.delay_line_set (in + lp * g, 0, chnl);

  this.filt_state[chnl] = lp;
  y;
);

// no interpolation
// dst_ptr = outs, in_ptr = ins, delay_spls_ptr = delays.
function delay_line_lp_comb_tick_cascade_all (in, active_count, delay_spls_ptr)
    local (i, g, y, y1, out) global() (

    this.delay_line_advance();
    i = 0;
    out = in;
    loop (active_count,
      out = this.delay_line_lp_comb_tick (out, i, delay_spls_ptr[i]);
      i += 1;
    );
    loop (this.n_channels - active_count,
      this.delay_line_lp_comb_tick (0, i, delay_spls_ptr[i]);
      i += 1;
    );
    out;
);

// no interpolation
// dst_ptr = outs, in_ptr = ins, delay_spls_ptr = delays.
function delay_line_lp_comb_tick_tapped_all (in, active_count, delay_spls_ptr)
    local (i, g, y, y1, next, out) global() (

    this.delay_line_advance();
    i = 0;
    next = in;
    out = in;
    loop (active_count,
      next = this.delay_line_lp_comb_tick (next, i, delay_spls_ptr[i]);
      out += next;
      i += 1;
    );
    loop (this.n_channels - active_count,
      this.delay_line_lp_comb_tick (0, i, delay_spls_ptr[i]);
      i += 1;
    );
    out / (active_count + 1);
);

// this.n_channels has to be a multiple of "n_groups"
function delay_line_lp_comb_tick_tapped_parallel_all(
    in, n_groups, delay_spls_ptr
    )
    local (total, groupsz, partial, i) global()(

    this.delay_line_advance();

    total = 0;
    groupsz = floor (this.n_channels / n_groups);

    i = 0;
    loop (n_groups,
      partial = 0;
      loop (groupsz,
        partial += this.delay_line_lp_comb_tick (in, i, delay_spls_ptr[i]);
        i += 1;
      );
      total += partial;
    );
    total / i;
);
//------------------------------------------------------------------------------
