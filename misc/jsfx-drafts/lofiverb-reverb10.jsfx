//desc: large hall
slider1:decay_p=0.8<0,1,0.01> Decay

slider3:mod=0.5<0,1,0.01> Mod
slider4:character=0.5<0,1,0.01> Character

slider6:hi=0.5<0,1,0.01> highs
slider7:lo=0.5<0,1,0.01> lows

slider9:stereo=1<-1,1, 0.01> Stereo
slider10:predelay=1<1, 2400, 1> Predelay

slider21:db_wet=-12<-40, 12,0.1> Wet dB
slider20:db_dry=0<-60, 0,0.1> Dry dB

slider23:offset=0<-50, 50, 1> Offset

import _core.jsfx-inc
import _easy_allpass_reverb.jsfx-inc
import _easy_fdn.jsfx-inc
import _reverb.jsfx-inc
import _lfo.jsfx-inc
import _fir.jsfx-inc
import _filters.jsfx-inc

@init

// SRC stuff -------------------------------------------------------------------
BYPASS_SRC = 0;

k.src.kaiser_att_db = 210;
k.src.taps_branch = 32;
k.src.taps_branch_frac = 16;
k.src.cutoff = 10500;
k.src.srate = BYPASS_SRC ? srate : 23400;

src.down.resampler_2c_init(
  k.src.srate,
  srate,
  k.src.taps_branch,
  k.src.taps_branch_frac,
  k.src.cutoff,
  k.src.kaiser_att_db,
  1
  );

src.up.resampler_2c_init(
  srate,
  k.src.srate,
  k.src.taps_branch,
  k.src.taps_branch_frac,
  k.src.cutoff,
  k.src.kaiser_att_db,
  1
  );

src.out_q.deque_init(
  src.up.resampler_2c_max_n_out_samples() * 2
  );

src.ins.array_init (src.down.resampler_2c_max_n_out_samples() * 2);
src.outs.array_init (src.up.resampler_2c_max_n_out_samples() * 2);

src.ins.array_clear();
src.outs.array_clear();

mtxa.array_init (8);
mtxb.array_init (8);

// TODO how many?
loop(floor (srate / 44100) * 2,
  src.out_q.deque_push (0);
  src.out_q.deque_push (0);
);

// Actual reverb stuff ---------------------------------------------------------
n_lfos = 2;
lfo1.lfo_bank_init (n_lfos, 1);
lfo1.ret.array_init (n_lfos);
lfo1.lfo_bank_reset_phase (0);

lfo2.lfo_bank_init (n_lfos, 1);
lfo2.ret.array_init (n_lfos);
lfo2.lfo_bank_reset_phase (0);

eq1l.svf_bell (400, 0.78, 2.5, k.src.srate);
eq1r.svf_bell (400, 0.78, 2.5, k.src.srate);

@slider

lfo1.lfo_bank_reset_freq (0, 1.73 - mod * 0.53, k.src.srate);
lfo2.lfo_bank_reset_freq (0, 1.51 - mod * 0.43, k.src.srate);

dry_gain  = (dry_gain == -60) ? 0. : db_to_lin (db_dry);
wet_gain = db_to_lin (db_wet);

fg_decay = 1 - decay_p;
fg_decay *= fg_decay;
fg_decay = 1 - fg_decay;

decay = 0.3 + decay_p * decay_p * 4.75;

k1 = 0.08 + decay_p * 0.1;
k2 = 0.08 + decay_p * 0.08;
k3 = 0.05;
k4 = 0.15 + fg_decay * 0.15 + character * 0.2;

flo = 0.90 + lo * lo * 0.05;
glo = 0.85 + lo * 0.145;
fhi1 = 0.9 - hi * hi * 0.4;
fhi2 = 0.93 - hi * hi * 0.4;
ghi = 0.65 + hi * 0.35;

// df = 0.878;
df = 0.906;
// df = 1;
df1 = 3 * (.01 + character * 0.99);

@sample

// decay = 1;

// Downsampling
n_spls_down = src.down.resampler_2c_tick (src.ins.ptr, spl0, spl1);

(n_spls_down) ? (
  lfo1.lfo_bank_tick_tri (lfo1.ret.ptr, 0);
  lfo2.lfo_bank_tick_tri (lfo2.ret.ptr, 0);


  // halving an additional time to leave headroom
  li = src.ins.ptr[0] * 0.5;
  ri = src.ins.ptr[1] * 0.5;

  li = fil1.lp (li, 0.15);
  li = fil2.hp (li, 0.99);
  ri = fir1.lp (ri, 0.15);
  ri = fir2.hp (ri, 0.99);

  m = (li + ri) * 0.5;
  s = (li - ri) * 0.5;

  l = r = 0;

  v1 = m + fb1;
  fb1 = delay_fb (v1, 1022 * df , decay, k.src.srate);
  fb1 = fb1.crossv2 (fb1, flo, fhi1, glo, ghi);
  fb1 = ap3 (fb1, 781 * df, 436 * df, 312 * df, -k1, k2, -k3);
  l += delay (v1, 97) * 0.75;
  r += delay (v1, 112) * 0.25;
  l -= delay (v1, 342, lfo1.ret.ptr[0], 48) * 0.75;
  r += delay (v1, 377, lfo1.ret.ptr[1], 41) * 0.5;
  l += delay (v1, 474 - 46, lfo1.ret.ptr[0] * mod, 89) * 0.125;
  r -= delay (v1, 472 - 45, lfo1.ret.ptr[1] * mod, 73) * 0.125;
  l += delay (v1, 522, lfo1.ret.ptr[0] * mod, 27) * 0.5;
  r -= delay (v1, 540, lfo1.ret.ptr[1] * mod, 28) * 0.25;

  v2 = m - fb2; // BEWARE THIS SUB!
  fb2 = delay_fb (v2, 1053 * df, decay, k.src.srate);
  fb2 = fb2.crossv2 (fb2, flo, fhi2, glo, ghi);
  fb2 = ap3 (fb2, 807 * df, 445 * df, 316* df, -k1, -k2, k3);
  l += delay (v2, 170) * 0.5;
  r -= delay (v2, 160) * 0.75;
  l -= delay (v2, 396, lfo1.ret.ptr[1] * mod, 77) * 0.75;
  r += delay (v2, 397, lfo1.ret.ptr[0] * mod, 88) * 0.25;
  l += delay (v2, 438, lfo1.ret.ptr[1], 16) * 0.25;
  r -= delay (v2, 471, lfo1.ret.ptr[0], 14) * 0.5;
  l += delay (v2, 512 - 18, lfo1.ret.ptr[1] * mod, 89) * 0.125;
  r -= delay (v2, 513 - 18, lfo1.ret.ptr[0] * mod, 73) * 0.125;

  q1i = easyapv_del_idx;
  v3 = li + fb3;
  fb3 = delay_fb (v3, 955* df, decay, k.src.srate);
  fb3 = fb3.crossv2 (fb3, flo, fhi1, glo, ghi);
  fb3 = ap3 (fb3, 734* df, 420* df, 282* df, -k1, k2, k3);
  l -= delay (v3, 422) * 0.75;
  r -= delay (v3, 415) * 0.5;
  l += delay (v3, 646, lfo2.ret.ptr[1] * mod, 57) * 0.75;
  r -= delay (v3, 647, lfo2.ret.ptr[0] * mod, 58) * 0.5;
  l += delay (v3, 673, lfo2.ret.ptr[1], 7) * 0.125;
  r -= delay (v3, 671, lfo2.ret.ptr[0], 13) * 0.125;
  l -= delay (v3, 858, lfo2.ret.ptr[1] * mod, 89) * 0.25;
  r += delay (v3, 836, lfo2.ret.ptr[0] * mod, 73) * 0.25;

  q2i = easyapv_del_idx;
  v4 = ri + fb4;
  fb4 = delay_fb (v4, 1433* df, decay, k.src.srate);
  fb4 = fb4.crossv2 (fb4, flo, fhi2, glo, ghi);
  fb4 = ap3 (fb4, 1111* df, 635* df, 433* df, -k1, k2, k3);
  l += delay (v4, 267, lfo2.ret.ptr[0], 9) * 0.5;
  r -= delay (v4, 297, lfo2.ret.ptr[1], 10) * 0.75;
  l += delay (v4, 776 + 39) * 0.125;
  r += delay (v4, 775 + 39) * 0.125;
  l += delay (v4, 842, lfo2.ret.ptr[0] * mod, 39)  * 0.5;
  r += delay (v4, 877, lfo2.ret.ptr[1] * mod, 33)  * 0.75;
  l -= delay (v4, 1103, lfo2.ret.ptr[0 ] * mod, 77) * 0.25;
  r -= delay (v4, 1131, lfo2.ret.ptr[1] * mod, 98) * 0.25;

  l *= 0.1;
  r *= 0.1;

  l = ap (l, 32 + ((157 - 32)* df1), -k4);
  l = ap (l, 32 + ((243 - 32)* df1), k4);
  l = ap (l, 32 + ((373 - 32)* df1), k4);
  r = ap (r, 32 + ((167 - 32)* df1), -k4);
  r = ap (r, 32 + ((254 - 32)* df1), k4);
  r = ap (r, 32 + ((383 - 32)* df1), k4);

//  l = ap (l, 91, 0.4);
//  l = ap (l, 131, 0.4);
//  r = ap (r, 91, 0.4);
//  r = ap (r, 163, 0.4);

  l += easyapv_get (q1i, 215) * 0.1;
  r -= easyapv_get (q2i, 298) * 0.1;

  // more boilerplate (summing + downsample)
  easy_ap_next_sample();
  easy_fdn_next_sample();

  l = r * (1 - abs (stereo)) + l * abs (stereo);
  stereo < 0 ? (
    tmp = r;
    r = l;
    l = tmp;
  );

  l = eq1l.svf_tick (l);
  r = eq1r.svf_tick (r);

  // Upsampling
  n_spls_up = src.up.resampler_2c_tick (src.outs.ptr, l, r);
  i = 0;
  loop (n_spls_up * 2, // *2 : two channels
    src.out_q.deque_push (src.outs.ptr[i]);
    i += 1;
  );
);

spl0 *= dry_gain;
spl1 *= dry_gain;
spl0 += src.out_q.deque_pop() * wet_gain;
spl1 += src.out_q.deque_pop() * wet_gain;
