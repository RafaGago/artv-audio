//desc: large hall
slider1:decay_p=0.8<0,1,0.01> Decay

slider3:mod=0.5<0,1,0.01> Mod
slider4:character=0.5<0,1,0.01> Character

slider6:hi=0.5<0,1,0.01> highs
slider7:lo=0.5<0,1,0.01> lows

slider9:stereo=1<-1,1, 0.01> Stereo
slider10:predelay=1<1, 2400, 1> Predelay

slider21:db_wet=-12<-40, 12,0.1> Wet dB
slider20:db_dry=0<-60, 0,0.1> Dry dB

slider23:offset=0<-50, 50, 1> Offset

import _core.jsfx-inc
import _easy_allpass_reverb.jsfx-inc
import _easy_fdn.jsfx-inc
import _reverb.jsfx-inc
import _lfo.jsfx-inc
import _fir.jsfx-inc
import _filters.jsfx-inc

@init

// SRC stuff -------------------------------------------------------------------
BYPASS_SRC = 0;

k.src.kaiser_att_db = 210;
k.src.taps_branch = 32;
k.src.taps_branch_frac = 16;
k.src.cutoff = 10500;
k.src.srate = BYPASS_SRC ? srate : 23400;

src.down.resampler_2c_init(
  k.src.srate,
  srate,
  k.src.taps_branch,
  k.src.taps_branch_frac,
  k.src.cutoff,
  k.src.kaiser_att_db,
  1
  );

src.up.resampler_2c_init(
  srate,
  k.src.srate,
  k.src.taps_branch,
  k.src.taps_branch_frac,
  k.src.cutoff,
  k.src.kaiser_att_db,
  1
  );

src.out_q.deque_init(
  src.up.resampler_2c_max_n_out_samples() * 2
  );

src.ins.array_init (src.down.resampler_2c_max_n_out_samples() * 2);
src.outs.array_init (src.up.resampler_2c_max_n_out_samples() * 2);

src.ins.array_clear();
src.outs.array_clear();

mtxa.array_init (8);
mtxb.array_init (8);

// TODO how many?
loop(floor (srate / 44100) * 2,
  src.out_q.deque_push (0);
  src.out_q.deque_push (0);
);

// Actual reverb stuff ---------------------------------------------------------
n_lfos = 2;
lfo1.lfo_bank_init (n_lfos, 1);
lfo1.ret.array_init (n_lfos);
lfo1.lfo_bank_reset_phase (0);

lfo2.lfo_bank_init (n_lfos, 1);
lfo2.ret.array_init (n_lfos);
lfo2.lfo_bank_reset_phase (0);

eq1l.svf_bell (300, 0.64, 3, k.src.srate);
eq1r.svf_bell (300, 0.64, 3, k.src.srate);

@slider

lfo1.lfo_bank_reset_freq (0, 1.73 - mod * 0.63, k.src.srate);
lfo2.lfo_bank_reset_freq (0, 1.53 - mod * 0.43, k.src.srate);

dry_gain  = (dry_gain == -60) ? 0. : db_to_lin (db_dry);
wet_gain = db_to_lin (db_wet);

fg_decay = 1 - decay_p;
fg_decay *= fg_decay;
fg_decay = 1 - fg_decay;

decay = 0.35 + decay_p * decay_p * 4.75;

k1 = 0.12 + decay_p * 0.2;
k2 = 0.12 + decay_p * 0.1;
k3 = 0.1;
k4 = 0.1 + fg_decay * 0.1 + character * 0.3;

flo = 0.90 + lo * lo * 0.05;
glo = 0.85 + lo * 0.145;
fhi1 = 0.9 - hi * hi * 0.4;
fhi2 = 0.93 - hi * hi * 0.4;
ghi = 0.5 + hi * 0.5;

@sample

// decay = 1;

// Downsampling
n_spls_down = src.down.resampler_2c_tick (src.ins.ptr, spl0, spl1);

(n_spls_down) ? (
  lfo1.lfo_bank_tick_tri (lfo1.ret.ptr, 0);
  lfo2.lfo_bank_tick_tri (lfo2.ret.ptr, 0);

  // halving an additional time to leave headroom
  li = src.ins.ptr[0] * 0.5;
  ri = src.ins.ptr[1] * 0.5;

  li = fil1.lp (li, 0.16);
  li = fil2.hp (li, 0.99);
  ri = fir1.lp (ri, 0.16);
  ri = fir2.hp (ri, 0.99);

  li = ap (li, 123, k4);
  ri = ap (ri, 144, k4);

  m = (li + ri) * 0.5;
  s = (li - ri) * 0.5;

  l = r = 0;

  v1 = m + fb1;
  fb1 = delay_fb (v1, 1821, decay, k.src.srate);
  fb1 = fb1.crossv2 (fb1, flo, fhi1, glo, ghi);
  fb1 = ap3 (fb1, 912, 736, 515, k1, k2, k3);
  l += delay (v1, 112 + 2);
  r -= delay (v1, 112);
  l += delay (v1, 342, lfo1.ret.ptr[0], 13);
  r += delay (v1, 377, lfo1.ret.ptr[0], 16);
  l += delay (v1, 412, lfo1.ret.ptr[0] * mod, 77);
  r -= delay (v1, 410, lfo1.ret.ptr[0] * mod, 88);
  l += delay (v1, 1112 - 46, lfo1.ret.ptr[0] * mod, 89);
  r += delay (v1, 1112 - 45, lfo1.ret.ptr[0] * mod, 73);

  v2 = m + fb2;
  fb2 = delay_fb (v2, 1827 + 387, decay, k.src.srate);
  fb2 = fb2.crossv2 (fb2, flo, fhi2, glo, ghi);
  fb2 = ap3 (fb2, 772, 666, 535, k1, k2, k3);
  l -= delay (v2, 232);
  r += delay (v2, 230);
  l += delay (v2, 666, lfo1.ret.ptr[1] * mod, 77);
  r += delay (v2, 667, lfo1.ret.ptr[1] * mod, 88);
  l += delay (v2, 812, lfo1.ret.ptr[1], 21);
  r -= delay (v2, 831, lfo1.ret.ptr[1], 15);
  l -= delay (v2, 1312, lfo1.ret.ptr[1] * mod, 89);
  r += delay (v2, 1312, lfo1.ret.ptr[1] * mod, 73);

  q1i = easyapv_del_idx;
  v3 = (li + fb3);
  fb3 = delay_fb (v3, 1827 + 38 + 56, decay, k.src.srate);
  fb3 = fb3.crossv2 (fb3, flo, fhi1, glo, ghi);
  fb3 = ap3 (fb3, 672, 526, 425, k1, k2, k3);
  l -= delay (v3, 192);
  r += delay (v3, 196);
  l -= delay (v3, 376, lfo2.ret.ptr[0] * mod, 77);
  r += delay (v3, 377, lfo2.ret.ptr[0] * mod, 88);
  l -= delay (v3, 777, lfo2.ret.ptr[0] * mod, 89);
  r += delay (v3, 776, lfo2.ret.ptr[0] * mod, 73);
  l += delay (v3, 1472, lfo2.ret.ptr[0], 17);
  r += delay (v3, 1471, lfo2.ret.ptr[0], 23);

  q2i = easyapv_del_idx;
  v4 = (ri + fb4);
  fb4 = delay_fb (v4, 1827 + 38 + 56 + 37, decay, k.src.srate);
  fb4 = fb4.crossv2 (fb4, flo, fhi2, glo, ghi);
  fb4 = ap3 (fb4, 572, 426, 325, k1, k2, k3);
  l -= delay (v4, 377, lfo2.ret.ptr[1], 22);
  r += delay (v4, 376, lfo2.ret.ptr[1], 17);
  l += delay (v4, 642, lfo2.ret.ptr[1] * mod, 89);
  r -= delay (v4, 677, lfo2.ret.ptr[1] * mod, 73);
  l += delay (v4, 992, lfo2.ret.ptr[1] * mod, 77);
  r += delay (v4, 992, lfo2.ret.ptr[1] * mod, 98);
  l -= delay (v4, 1612);
  r += delay (v4, 1612);

  l *= 0.1;
  r *= 0.1;

+   l -= easyapv_get (q1i, 615) * 0.1;
  r += easyapv_get (q2i, 873) * 0.1;


//  l = ap3 (l, 972, 766, 525, 0.4, 0.3, 0.2);
//  r = ap3 (r, 972, 766, 525, 0.4, 0.3, 0.2);


//  l = r = (v1 + v2 + v3 + v4 + v5 + v6 + v7 + v8 + v9 + v10) * 0.2;

  // more boilerplate (summing + downsample)
  easy_ap_next_sample();
  easy_fdn_next_sample();

  l = r * (1 - abs (stereo)) + l * abs (stereo);
  stereo < 0 ? (
    tmp = r;
    r = l;
    l = tmp;
  );

  l = eq1l.svf_tick (l);
  r = eq1r.svf_tick (r);

  // Upsampling
  n_spls_up = src.up.resampler_2c_tick (src.outs.ptr, l, r);
  i = 0;
  loop (n_spls_up * 2, // *2 : two channels
    src.out_q.deque_push (src.outs.ptr[i]);
    i += 1;
  );
);

spl0 *= dry_gain;
spl1 *= dry_gain;
spl0 += src.out_q.deque_pop() * wet_gain;
spl1 += src.out_q.deque_pop() * wet_gain;
