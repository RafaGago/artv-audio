slider1:decay_p=0.8<0,1,0.01> Decay

slider3:mod=0.5<0,1,0.01> Mod
slider4:character=0.5<0,1,0.01> Character

slider6:hi=0.5<0,1,0.01> highs
slider7:lo=0.5<0,1,0.01> lows

slider9:stereo=1<-1,1, 0.01> Stereo
slider10:predelay=1<1, 2400, 1> Predelay

slider21:db_wet=-12<-40, 12,0.1> Wet dB
slider20:db_dry=0<-60, 0,0.1> Dry dB

slider23:offset=0<0, 30, 1> Offset

import _core.jsfx-inc
import _easy_allpass_reverb.jsfx-inc
import _easy_fdn.jsfx-inc
import _reverb.jsfx-inc
import _lfo.jsfx-inc
import _fir.jsfx-inc

@init

// SRC stuff -------------------------------------------------------------------
BYPASS_SRC = 0;

k.src.kaiser_att_db = 210;
k.src.taps_branch = 32;
k.src.taps_branch_frac = 16;
k.src.cutoff = 15000;
k.src.srate = BYPASS_SRC ? srate : 32400;

src.down.resampler_2c_init(
  k.src.srate,
  srate,
  k.src.taps_branch,
  k.src.taps_branch_frac,
  k.src.cutoff,
  k.src.kaiser_att_db,
  1
  );

src.up.resampler_2c_init(
  srate,
  k.src.srate,
  k.src.taps_branch,
  k.src.taps_branch_frac,
  k.src.cutoff,
  k.src.kaiser_att_db,
  1
  );

src.out_q.deque_init(
  src.up.resampler_2c_max_n_out_samples() * 2
  );

src.ins.array_init (src.down.resampler_2c_max_n_out_samples() * 2);
src.outs.array_init (src.up.resampler_2c_max_n_out_samples() * 2);

src.ins.array_clear();
src.outs.array_clear();

mtx.array_init (4);
mtxcp.array_init (4);

// TODO how many?
loop(floor (srate / 44100) * 2,
  src.out_q.deque_push (0);
  src.out_q.deque_push (0);
);

// Actual reverb stuff ---------------------------------------------------------
n_lfos = 4;
lfo.lfo_bank_init (n_lfos, 1);
lfo.ret.array_init (n_lfos);
lfo.lfo_bank_reset_phase (0);

@slider

lfo.lfo_bank_reset_freq (0, 0.25 + mod * 0.75, k.src.srate);

dry_gain  = (dry_gain == -60) ? 0. : db_to_lin (db_dry);
wet_gain = db_to_lin (db_wet);

decayfb = 0.4 + decay_p * 0.45;

sumgain1 = 0.125 * (0.8 + character * 0.4);
sumgain2 =  0.125 * (1.2 - character * 0.4);

flo = 0.90 + lo * lo * 0.05;
glo = 0.7 + lo * 0.3;
fhi = 0.82 - hi * hi * 0.4;
ghi = 0.6 + hi * 0.35;


@sample

// Downsampling
n_spls_down = src.down.resampler_2c_tick (src.ins.ptr, spl0, spl1);

(n_spls_down) ? (
  m_lfo.lfo_bank_tick_tri (m_lfo.ret.ptr, 0);
  lfo.lfo_bank_tick_tri (lfo.ret.ptr, 0);

  // https://gearspace.com/board/attachments/geekzone/1002937d1644118238-reverb-subculture-v2_reverbb.jpg


  // halving an additional time to leave headroom
  m = (src.ins.ptr[0] + src.ins.ptr[1]) * 0.5 * 0.5;
  m = fi1.lp (m, 0.08);
  m = fi2.hp (m, 0.99);
  ml = m;

  l = delay (ml, 42) * -sumgain1; // notice assign not sum
  r = delay (ml, 105) * sumgain1; // notice assign not sum
  r += delay (ml, 490) * -sumgain1;
  l += delay (ml, 586) * sumgain1;
  l += delay (ml, 1099) * -sumgain1;
  r += delay (ml, 1290) * sumgain1;

  ci = ml + fb1;
  fb1 = fb1.crossv2 (ci, flo, fhi, glo, ghi);
  fb1 = delay (fb1, 5719 - 32, lfo.ret.ptr[0] * mod, 43) * -decayfb;
  fb1 = delay (fb1, 32);
  t = delay (ci, 640) * -sumgain2; // notice assign not sum
  t += delay (ci, 1494) * sumgain2;
  t += delay (ci, 2199) * -sumgain2;
  t += delay (ci, 3122) * sumgain2;
  t += delay (ci, 4135) * -sumgain2;
  t += delay (ci, 4952) * sumgain2;

  ci = ml + fb2;
  fb2 = fb2.crossv2 (ci, flo, fhi, glo, ghi);
  fb2 = delay (fb2, 5779-32, lfo.ret.ptr[1] * mod, 44) * -decayfb;
  fb2 = delay (fb2, 32);
  t += delay (ci, 902) * -sumgain2;
  t += delay (ci, 1830) * sumgain2;
  t += delay (ci, 2528) * -sumgain2;
  t += delay (ci, 3641) * sumgain2;
  t += delay (ci, 4670) * -sumgain2;
  t += delay (ci, 5432) * sumgain2;

  ci = ml + fb3;
  fb3 = fb3.crossv2 (ci, flo, fhi, glo, ghi);
  fb3 = delay (fb3, 5905 -32, lfo.ret.ptr[2] * mod, 42) * -decayfb;
  fb3 = delay (fb3, 32);
  t += delay (ci, 979) * -sumgain2;
  t += delay (ci, 2031) * sumgain2; // notice assign not sum
  t += delay (ci, 3267) * -sumgain2;
  t += delay (ci, 4104) * sumgain2;
  t += delay (ci, 5103) * -sumgain2;
  t += delay (ci, 5886) * sumgain2;

  eramt = abs ((character - 0.5)) * 2;
  eramt = 1 - eramt * eramt;
  eramt = 0.4 * eramt;
  mainamt = 1 - eramt;

  l = ap (t, 224, -0.7);
  l = delay (ml, 311, character, 311) * eramt + l * mainamt;
  l = ap (l, 227, -0.7);
  l = ap (l, 1343, -0.7);

  r = ap (t, 224, -0.7);
  r = delay (ml, 277, character, 400) * eramt + r * mainamt;
  r = ap (r, 91, -0.7);
  r = ap (r, 1182, -0.7);
 

  // more boilerplate (summing + downsample)
  easy_ap_next_sample();
  easy_fdn_next_sample();

  l = r * (1 - abs (stereo)) + l * abs (stereo);
  stereo < 0 ? (
    tmp = r;
    r = l;
    l = tmp;
  );

  // Upsampling
  n_spls_up = src.up.resampler_2c_tick (src.outs.ptr, l, r);
  i = 0;
  loop (n_spls_up * 2, // *2 : two channels
    src.out_q.deque_push (src.outs.ptr[i]);
    i += 1;
  );
);

spl0 *= dry_gain;
spl1 *= dry_gain;
spl0 += src.out_q.deque_pop() * wet_gain;
spl1 += src.out_q.deque_pop() * wet_gain;
